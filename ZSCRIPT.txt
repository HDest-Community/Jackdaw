version "4.0"

class JackdawHandler : EventHandler
{
	override void CheckReplacement(ReplaceEvent e)
	{
		if (!e.Replacement)
		{
			return;
		}

		switch (e.Replacement.GetClassName())
		{
			case 'ChaingunReplaces':
				if (random[jackrand]() <= 64)
				{
					e.Replacement = "JackdawRandom";
				}
				break;
		}
	}

	override void WorldThingSpawned(WorldEvent e)
	{
		let JackdawAmmo = HDPistolAmmo(e.Thing);
		if (JackdawAmmo && JackdawAmmo.GetClassName() == 'HDPistolAmmo')
		{
			JackdawAmmo.ItemsThatUseThis.Push("HDJackdaw");
		}
	}
}

class HDJackdaw : HDWeapon
{
	enum JackdawFlags
	{
		JDF_JustUnload = 1,
		JDF_RapidFire = 2
	}

	enum JackdawProperties
	{
		JDProp_Flags,
		JDProp_Chamber,
		JDProp_Mag, // [Ace] Just so I don't have to rewrite the entirety of the code, the mag will be loaded with up to 30 rounds when the gun is equipped.
	}

	override bool AddSpareWeapon(actor newowner) { return AddSpareWeaponRegular(newowner); }
	override HDWeapon GetSpareWeapon(actor newowner, bool reverse, bool doselect) { return GetSpareWeaponRegular(newowner, reverse, doselect); }
	override double GunMass()
	{
		return 10 + ENC_9_LOADED * WeaponStatus[JDProp_Mag];
	}
	override double WeaponBulk()
	{
		double BaseBulk = 134;
		int Mag = WeaponStatus[JDProp_Mag];
		if (Mag >= 0)
		{
			BaseBulk += Mag * ENC_9_LOADED;
		}
		return BaseBulk;
	}
	override string, double GetPickupSprite() { return "JDWGZ0", 0.52; }
	override void InitializeWepStats(bool idfa)
	{
		WeaponStatus[JDProp_Chamber] = 2;
		WeaponStatus[JDProp_Mag] = 30;
	}
	override void LoadoutConfigure(string input)
	{
		if (GetLoadoutVar(input, "rapid", 1) > 0)
		{
			WeaponStatus[JDProp_Flags] |= JDF_RapidFire;
		}

		InitializeWepStats(false);
	}

	override string GetHelpText()
	{
		return WEPHELP_FIRESHOOT
		..WEPHELP_RELOAD.."  Reload mag\n"
		..WEPHELP_UNLOADUNLOAD
		..WEPHELP_USE.."+"..WEPHELP_RELOAD.."  Reload chamber\n"
		..WEPHELP_MAGMANAGER;
	}

	override string PickupMessage()
	{
		string RapidStr = WeaponStatus[JDProp_Flags] & JDF_RapidFire ? "rapid-fire " : "";
		return String.Format("You picked up the %s'Jackdaw' 9mm SMG.", RapidStr);
	}

	override void DrawHUDStuff(HDStatusBar sb, HDWeapon hdw, HDPlayerPawn hpl)
	{
		sb.DrawWepNum(hdw.WeaponStatus[JDProp_Mag], 30);

		if (hdw.WeaponStatus[JDProp_Chamber] == 2)
		{
			sb.DrawRect(-19, -11, 3, 1);
		}
	}

	override void DrawSightPicture(HDStatusBar sb, HDWeapon hdw, HDPlayerPawn hpl, bool sightbob, vector2 bob, double fov, bool scopeview, actor hpc, string whichdot)
	{
		int cx, cy, cw, ch;
		[cx, cy, cw, ch] = Screen.GetClipRect();
		sb.SetClipRect(-16 + bob.x, -4 + bob.y, 32, 16, sb.DI_SCREEN_CENTER);
		vector2 bob2 = bob * 2;
		bob2.y = clamp(bob2.y, -8, 8);
		sb.DrawImage("JDWFRONT", bob2, sb.DI_SCREEN_CENTER | sb.DI_ITEM_TOP, alpha: 0.9);
		sb.SetClipRect(cx, cy, cw, ch);
		sb.DrawImage("JDWBACK", (0, -7) + bob, sb.DI_SCREEN_CENTER | sb.DI_ITEM_TOP);
	}

	private action void A_TryLoadMag()
	{
		if (invoker.Storage && invoker.Storage.owner == invoker.owner && invoker.Storage.Storage)
		{
			if (invoker.WeaponStatus[JDProp_Mag] == 0)
			{
				let nma = invoker.Storage.Storage.Find('HDPistolAmmo');
				if (nma)
				{
					int ToAdd = min(30 - invoker.WeaponStatus[JDProp_Mag], invoker.Storage.Storage.GetAmount('HDPistolAmmo'));
					invoker.WeaponStatus[JDProp_Mag] += ToAdd;

					if (ToAdd > 0)
					{
						invoker.Storage.Storage.RemoveItem(nma, null, null, ToAdd);
					}
				}
				else
				{
					A_FindStorage();
				}
			}
			return;
		}

		A_FindStorage();
	}

	private action void A_FindStorage()
	{
		for (Inventory Next = Inv; Next; Next = Next.Inv)
		{
			let bp = HDBackpack(Next);
			if (bp && bp.Storage)
			{
				let nma = bp.Storage.Find('HDPistolAmmo');
				if (nma)
				{
					invoker.Storage = bp;
					break;
				}
			}
		}
	}

	private HDBackpack Storage;

	Default
	{
		-HDWEAPON.FITSINBACKPACK
		Weapon.SelectionOrder 300;
		Weapon.SlotNumber 4;
		Weapon.SlotPriority 1.5;
		HDWeapon.BarrelSize 25, 2, 4;
		Scale 0.28;
		Tag "'Jackdaw' 9mm SMG";
		HDWeapon.Refid "jdw";
	}

	States
	{
		Spawn:
			JDWG Z -1;
			Stop;
		Ready:
			JDWG A 1
			{
				A_TryLoadMag();
				A_WeaponReady(WRF_ALL);
			}
			Goto ReadyEnd;
		Select0:
			JDWG A 0;
			Goto Select0Big;
		Deselect0:
			JDWG A 0;
			Goto Deselect0Big;

		AltFire:
			Goto ChamberManual;

		Fire:
			JDWG A 1
			{
				if (invoker.WeaponStatus[JDProp_Chamber] < 2)
				{
					SetWeaponState("ChamberManual");
					return;
				}
			}
			JDWF A 2 Offset(0, 34)
			{
				if (invoker.WeaponStatus[JDProp_Flags] & JDF_RapidFire)
				{
					A_SetTics(1);
				}

				let Proj = HDBulletActor.FireBullet(self, "HDB_9", spread: 2.0, speedfactor: 1.15);
				if (frandom(24, ceilingz - floorz) < Proj.speed * 0.1)
				{
					A_AlertMonsters(250);
				}
				invoker.WeaponStatus[JDProp_Chamber] = 1;
				A_StartSound("Jackdaw/Fire", CHAN_WEAPON, volume: 0.7);
				A_ZoomRecoil(0.995);
				A_MuzzleClimb(-frandom(0.1, 0.12), -frandom(0.15, 0.18), -frandom(0.1, 0.12),-frandom(0.15, 0.18));
				A_Light1();
			}
			JDWG A 0
			{
				if (invoker.WeaponStatus[JDProp_Chamber] == 1)
				{
					A_SpawnItemEx("HDSpent9mm", cos(pitch) * 10, 0, height - 10 - sin(pitch) * 10, vel.x, vel.y, vel.z, 0, SXF_ABSOLUTEMOMENTUM | SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH);
					invoker.WeaponStatus[JDProp_Chamber] = 0;
				}

				if (invoker.WeaponStatus[JDProp_Mag] <= 0)
				{
					SetWeaponState("Nope");
				}
				else
				{
					A_Light0();
					invoker.WeaponStatus[JDProp_Chamber] = 2;
					invoker.WeaponStatus[JDProp_Mag]--;
				}
			}
			Goto Ready;

		Reload:
			JDWG A 0
			{
				if (invoker.WeaponStatus[JDProp_Mag] <= 0 && PressingUse() && CheckInventory("HDPistolAmmo", 1))
				{
					SetWeaponState("LoadChamber");
				}
			}
			Goto Nope;
		LoadChamber:
			JDWG A 0 A_JumpIf(invoker.WeaponStatus[JDProp_Chamber] > 0, "Nope");
			JDWG A 0 A_JumpIf(!CheckInventory("HDPistolAmmo", 1), "Nope");
			JDWG A 1 Offset(0, 34) A_StartSound("weapons/pocket", 9);
			JDWG A 1 Offset(2, 36);
			JDWG A 1 Offset(2, 44);
			JDWG A 1 Offset(5, 54);
			JDWG A 2 Offset(7, 60);
			JDWG A 6 Offset(8, 70);
			JDWG A 5 Offset(8, 77)
			{
				if (CheckInventory("HDPistolAmmo", 1))
				{
					A_TakeInventory("HDPistolAmmo", 1, TIF_NOTAKEINFINITE);
					invoker.WeaponStatus[JDProp_Chamber] = 2;
					A_StartSound("weapons/smgchamber", 8);
				}
				else
				{
					A_SetTics(4);
				}
			}
			JDWG A 3 Offset(9, 74);
			JDWG A 2 Offset(5, 70);
			JDWG A 1 Offset(5, 64);
			JDWG A 1 Offset(5, 52);
			JDWG A 1 Offset(5, 42);
			JDWG A 1 Offset(2, 36);
			JDWG A 2 Offset(0, 34);
			Goto Nope;

		ChamberManual:
			JDWG A 0 A_JumpIf(invoker.WeaponStatus[JDProp_Mag] <= 0 || invoker.WeaponStatus[JDProp_Chamber] == 2, "Nope");
			JDWG A 2 Offset(2, 34);
			JDWG A 4 Offset(3, 38) A_StartSound("Jackdaw/BoltPull", 8, CHANF_OVERLAP);
			JDWG A 5 Offset(4, 44)
			{
				if (invoker.WeaponStatus[JDProp_Chamber] == 1)
				{
					A_SpawnItemEx("HDSpent9mm", cos(pitch) * 10, 0, height - 10 - sin(pitch) * 10, vel.x, vel.y, vel.z, 0, SXF_ABSOLUTEMOMENTUM | SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH);
					invoker.WeaponStatus[JDProp_Chamber] = 0;
				}

				A_WeaponBusy();
				invoker.WeaponStatus[JDProp_Mag]--;
				invoker.WeaponStatus[JDProp_Chamber] = 2;
			}
			JDWG A 2 Offset(3, 38);
			JDWG A 2 Offset(2, 34);
			JDWG A 2 Offset(0, 32);
			Goto Nope;
	}
}

class JackdawRandom : IdleDummy
{
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				let wpn = HDJackdaw(Spawn("HDJackdaw", pos, ALLOW_REPLACE));
				if (!wpn)
				{
					return;
				}

				wpn.special = special;
				for (int i = 0; i < 5; ++i)
				{
					wpn.Args[i] = Args[i];
				}
				if (!random(0, 3))
				{
					wpn.WeaponStatus[wpn.JDProp_Flags] |= wpn.JDF_RapidFire;
				}
				wpn.InitializeWepStats(false);
			}
			Stop;
	}
}